<!DOCTYPE HMTL>
<html>
  <head>
    <title>webgl ray-marching</title>
    <link type="text/css" href="style.css" rel="stylesheet" />
  </head>
  <body>
    <div id="container">
      <canvas height="240" width="320" id="example"></canvas>
      <div id="framerate"></div>
      <script type="x-shader/x-vertex" id="vshader">
precision mediump float;
precision mediump int;

uniform mat4 u_projMatrix;
uniform mat4 u_modelViewMatrix;
uniform mat4 u_normalMatrix;
uniform vec3 lightDir;

attribute vec3 vNormal;
attribute vec4 vTexCoord;
attribute vec4 vPosition;

varying float v_Dot;
varying vec2 v_texCoord;

void main()
{
  mat4 modelViewProjMatrix =  u_projMatrix * u_modelViewMatrix;
  gl_Position = modelViewProjMatrix * vPosition;
  v_texCoord = vTexCoord.st;
  vec4 transNormal = u_normalMatrix * vec4(vNormal, 1);
  v_Dot = max(dot(transNormal.xyz, lightDir), 0.0);
}
  </script>
  <script type="x-shader/x-fragment" id="fshader">
precision mediump float;
precision mediump int;

uniform sampler2D colorSampler;
// simplex noise textures (lookup tables)
uniform sampler2D permTexture;
uniform sampler2D simplexTexture;
uniform sampler2D gradTexture;


uniform vec3 sphere1;
uniform vec3 sphere2;
uniform vec3 sphere3;
uniform float threshold;
uniform float time;

varying float v_Dot;
varying vec2 v_texCoord;
const float near = 0.1;
const float far = 2.0;
const float totalDepth = near + far; // abs(near) + abs(far) == total frustum depth
const int maxS = 21; // totalDepth / interval
const int maxB = 10; // max root-finding bisections to perform on interval containing root

// begin simplex noise code

// The skewing and unskewing factors are much simpler for the 3D case
const float F3 = 0.333333333333;
const float G3 = 0.166666666667;

// The skewing and unskewing factors are hairy again for the 4D case
// This is (sqrt(5.0)-1.0)/4.0
const float F4 = 0.309016994375;
// This is (5.0-sqrt(5.0))/20.0
const float G4 = 0.138196601125;

// To create offsets of one texel and one half texel in the
// texture lookup, we need to know the texture image size.
const float ONE = 0.00390625;
const float ONEHALF = 0.001953125;
// The numbers above are 1/256 and 0.5/256, change accordingly
// if you change the code to use another texture size.

// 3D simplex noise. Comparable in speed to classic noise, better looking.
float snoise(vec3 P) {
  // Skew the (x,y,z) space to determine which cell of 6 simplices we're in
  float s = (P.x + P.y + P.z) * F3; // Factor for 3D skewing
  vec3 Pi = floor(P + s);
  float t = (Pi.x + Pi.y + Pi.z) * G3;
  vec3 P0 = Pi - t; // Unskew the cell origin back to (x,y,z) space
  Pi = Pi * ONE + ONEHALF; // Integer part, scaled and offset for texture lookup

  vec3 Pf0 = P - P0;  // The x,y distances from the cell origin

  // For the 3D case, the simplex shape is a slightly irregular tetrahedron.
  // To find out which of the six possible tetrahedra we're in, we need to
  // determine the magnitude ordering of x, y and z components of Pf0.
  // The method below is explained briefly in the C code. It uses a small
  // 1D texture as a lookup table. The table is designed to work for both
  // 3D and 4D noise, so only 8 (only 6, actually) of the 64 indices are
  // used here.
  float c1 = (Pf0.x > Pf0.y) ? 0.5078125 : 0.0078125; // 1/2 + 1/128
  float c2 = (Pf0.x > Pf0.z) ? 0.25 : 0.0;
  float c3 = (Pf0.y > Pf0.z) ? 0.125 : 0.0;
  vec2 sindex = vec2(c1 + c2 + c3, 0);
  vec3 offsets = texture2D(simplexTexture, sindex).rgb;
  vec3 o1 = step(0.375, offsets);
  vec3 o2 = step(0.125, offsets);

  // Noise contribution from simplex origin
  float perm0 = texture2D(permTexture, Pi.xy).a;
  vec3  grad0 = texture2D(permTexture, vec2(perm0, Pi.z)).rgb * 4.0 - 1.0;
  float t0 = 0.6 - dot(Pf0, Pf0);
  float n0;
  if (t0 < 0.0) n0 = 0.0;
  else {
    t0 *= t0;
    n0 = t0 * t0 * dot(grad0, Pf0);
  }

  // Noise contribution from second corner
  vec3 Pf1 = Pf0 - o1 + G3;
  float perm1 = texture2D(permTexture, Pi.xy + o1.xy*ONE).a;
  vec3  grad1 = texture2D(permTexture, vec2(perm1, Pi.z + o1.z*ONE)).rgb * 4.0 - 1.0;
  float t1 = 0.6 - dot(Pf1, Pf1);
  float n1;
  if (t1 < 0.0) n1 = 0.0;
  else {
    t1 *= t1;
    n1 = t1 * t1 * dot(grad1, Pf1);
  }

  // Noise contribution from third corner
  vec3 Pf2 = Pf0 - o2 + 2.0 * G3;
  float perm2 = texture2D(permTexture, Pi.xy + o2.xy*ONE).a;
  vec3  grad2 = texture2D(permTexture, vec2(perm2, Pi.z + o2.z*ONE)).rgb * 4.0 - 1.0;
  float t2 = 0.6 - dot(Pf2, Pf2);
  float n2;
  if (t2 < 0.0) n2 = 0.0;
  else {
    t2 *= t2;
    n2 = t2 * t2 * dot(grad2, Pf2);
  }

  // Noise contribution from last corner
  vec3 Pf3 = Pf0 - vec3(1.0-3.0*G3);
  float perm3 = texture2D(permTexture, Pi.xy + vec2(ONE, ONE)).a;
  vec3  grad3 = texture2D(permTexture, vec2(perm3, Pi.z + ONE)).rgb * 4.0 - 1.0;
  float t3 = 0.6 - dot(Pf3, Pf3);
  float n3;
  if(t3 < 0.0) n3 = 0.0;
  else {
    t3 *= t3;
    n3 = t3 * t3 * dot(grad3, Pf3);
  }

  // Sum up and scale the result to cover the range [-1,1]
  return 32.0 * (n0 + n1 + n2 + n3);
}

// 4D simplex noise. A lot faster than classic 4D noise, and better looking.
float snoise(vec4 P) {
  // Skew the (x,y,z,w) space to determine which cell of 24 simplices we're in
  float s = (P.x + P.y + P.z + P.w) * F4; // Factor for 4D skewing
  vec4 Pi = floor(P + s);
  float t = (Pi.x + Pi.y + Pi.z + Pi.w) * G4;
  vec4 P0 = Pi - t; // Unskew the cell origin back to (x,y,z,w) space
  Pi = Pi * ONE + ONEHALF; // Integer part, scaled and offset for texture lookup

  vec4 Pf0 = P - P0;  // The x,y distances from the cell origin

  // For the 4D case, the simplex is a 4D shape I won't even try to describe.
  // To find out which of the 24 possible simplices we're in, we need to
  // determine the magnitude ordering of x, y, z and w components of Pf0.
  // The method below is presented without explanation. It uses a small 1D
  // texture as a lookup table. The table is designed to work for both
  // 3D and 4D noise and contains 64 indices, of which only 24 are actually
  // used. An extension to 5D would require a larger texture here.
  float c1 = (Pf0.x > Pf0.y) ? 0.5078125 : 0.0078125; // 1/2 + 1/128
  float c2 = (Pf0.x > Pf0.z) ? 0.25 : 0.0;
  float c3 = (Pf0.y > Pf0.z) ? 0.125 : 0.0;
  float c4 = (Pf0.x > Pf0.w) ? 0.0625 : 0.0;
  float c5 = (Pf0.y > Pf0.w) ? 0.03125 : 0.0;
  float c6 = (Pf0.z > Pf0.w) ? 0.015625 : 0.0;
  vec2 sindex = vec2(c1 + c2 + c3 + c4 + c5 + c6, 0);
  vec4 offsets = texture2D(simplexTexture, sindex).rgba;
  vec4 o1 = step(0.625, offsets);
  vec4 o2 = step(0.375, offsets);
  vec4 o3 = step(0.125, offsets);

  // Noise contribution from simplex origin
  float perm0xy = texture2D(permTexture, Pi.xy).a;
  float perm0zw = texture2D(permTexture, Pi.zw).a;
  vec4  grad0 = texture2D(gradTexture, vec2(perm0xy, perm0zw)).rgba * 4.0 - 1.0;
  float t0 = 0.6 - dot(Pf0, Pf0);
  float n0;
  if (t0 < 0.0) n0 = 0.0;
  else {
    t0 *= t0;
    n0 = t0 * t0 * dot(grad0, Pf0);
  }

  // Noise contribution from second corner
  vec4 Pf1 = Pf0 - o1 + G4;
  o1 = o1 * ONE;
  float perm1xy = texture2D(permTexture, Pi.xy + o1.xy).a;
  float perm1zw = texture2D(permTexture, Pi.zw + o1.zw).a;
  vec4  grad1 = texture2D(gradTexture, vec2(perm1xy, perm1zw)).rgba * 4.0 - 1.0;
  float t1 = 0.6 - dot(Pf1, Pf1);
  float n1;
  if (t1 < 0.0) n1 = 0.0;
  else {
    t1 *= t1;
    n1 = t1 * t1 * dot(grad1, Pf1);
  }

  // Noise contribution from third corner
  vec4 Pf2 = Pf0 - o2 + 2.0 * G4;
  o2 = o2 * ONE;
  float perm2xy = texture2D(permTexture, Pi.xy + o2.xy).a;
  float perm2zw = texture2D(permTexture, Pi.zw + o2.zw).a;
  vec4  grad2 = texture2D(gradTexture, vec2(perm2xy, perm2zw)).rgba * 4.0 - 1.0;
  float t2 = 0.6 - dot(Pf2, Pf2);
  float n2;
  if (t2 < 0.0) n2 = 0.0;
  else {
    t2 *= t2;
    n2 = t2 * t2 * dot(grad2, Pf2);
  }

  // Noise contribution from fourth corner
  vec4 Pf3 = Pf0 - o3 + 3.0 * G4;
  o3 = o3 * ONE;
  float perm3xy = texture2D(permTexture, Pi.xy + o3.xy).a;
  float perm3zw = texture2D(permTexture, Pi.zw + o3.zw).a;
  vec4  grad3 = texture2D(gradTexture, vec2(perm3xy, perm3zw)).rgba * 4.0 - 1.0;
  float t3 = 0.6 - dot(Pf3, Pf3);
  float n3;
  if (t3 < 0.0) n3 = 0.0;
  else {
    t3 *= t3;
    n3 = t3 * t3 * dot(grad3, Pf3);
  }

  // Noise contribution from last corner
  vec4 Pf4 = Pf0 - vec4(1.0-4.0*G4);
  float perm4xy = texture2D(permTexture, Pi.xy + vec2(ONE, ONE)).a;
  float perm4zw = texture2D(permTexture, Pi.zw + vec2(ONE, ONE)).a;
  vec4  grad4 = texture2D(gradTexture, vec2(perm4xy, perm4zw)).rgba * 4.0 - 1.0;
  float t4 = 0.6 - dot(Pf4, Pf4);
  float n4;
  if(t4 < 0.0) n4 = 0.0;
  else {
    t4 *= t4;
    n4 = t4 * t4 * dot(grad4, Pf4);
  }

  // Sum up and scale the result to cover the range [-1,1]
  return 27.0 * (n0 + n1 + n2 + n3 + n4);
}
// end simplex noise code

// point = (x,y,z)
// center = (a,b,c)
// potential(x,y,z) = s / ((x - a)^2 + (y - b)^2 + (z - c)^2)
float potential(vec3 center, vec3 point, float s) {
  vec3 tc = center - point; // to center
  return s / dot(tc, tc);
}

// point = (x,y,z)
// center = (a,b,c)
// gradient(x,y,z) = s / ((x - a)^2 + (y - b)^2 + (z - c)^2)^2 * (2(a-x), 2(b-y), 2(c-z))
vec3 gradient(vec3 center, vec3 point, float s) {
  vec3 tc = center - point; // to center
  vec3 ct = point - center; // to point
  float denom = dot(tc, tc);
  denom = denom*denom;
  return ct * (2.0 * s / denom);
}

float sampleField(vec3 point) {
  float pot1 = potential(sphere1, point,0.6);
  float pot2 = potential(sphere2, point,0.3);
  float pot3 = potential(sphere3, point,0.1);
  float potSum = pot1 + pot2 + pot3 - threshold;
  return potSum;
}

vec3 sampleGradient(vec3 point) {
  vec3 g1 = gradient(sphere1, point,0.6);
  vec3 g2 = gradient(sphere2, point,0.3);
  vec3 g3 = gradient(sphere3, point,0.1);
  return normalize(g1 + g2 + g3);
}

// dir is assumed to be normalized, returns ray origin when called with t=0
vec3 pointOnRay(vec3 origin, vec3 dir, float t) {
  return origin + (dir * t);
}

void main()
{
  const vec3 light0 = vec3(-1.0, 1.0, 2.0);
  vec3 toLight0 = vec3(0.0, 0.0, 1.0);
  vec2 texCoord = v_texCoord;
  vec3 pixelCoord = vec3(texCoord, -1.0);
  vec4 color = vec4(0.0, 0.0, 0.0, 0.0);
  vec3 rayOrigin = vec3(texCoord, near) + snoise(vec4(texCoord.xy * 8.0, 0.0, time * 0.1));
  vec3 rayDir = vec3(0.0, 0.0, -1.0);
  float t = near;
  vec3 beg = pointOnRay(rayOrigin, rayDir, t);
  float begPot = sampleField(beg);
  float interval = 0.1;
  vec3 mid = beg;
  float midPot = begPot;
  // ray march to find nearest interval containing a root
  for(int step = 0; step < maxS; step++) {
    vec3 end = pointOnRay(rayOrigin, rayDir, t+interval);
    float endPot = sampleField(end);
    if(begPot * endPot < 0.0) {
      // root exists, now refine root estimate over 10 bisection steps
      for(int b = 0; b < maxB; b++) {
        interval = interval / 2.0; // setup for next loop through
        mid = pointOnRay(rayOrigin, rayDir, t + interval);
        midPot = sampleField(mid);
        if(begPot * midPot < 0.0) {
          // root is in first half
        } else {
          // root is in second half
          beg = mid;
          begPot = midPot;
          t = t + interval; // step forward by the halved interval
        }
      }
      float v = 1.0 - mid.z*mid.z + 0.5;
      vec3 n = sampleGradient(mid);
      float cos_angle = dot(n, normalize(light0 - mid));

      color = vec4(1.0 * cos_angle, 0.0, 0.0, 1.0);
      break;
    }
    t = t + interval;
    beg = end;
  }

  gl_FragColor = color;
}

  </script>
  <script type="text/javascript" src="jquery.js"></script>
  <script type="text/javascript" src="J3DI.js"></script>
  <script type="text/javascript" src="J3DIMath.js"></script>
  <script type="text/javascript" src="app.js"></script>
</body>
</html>
